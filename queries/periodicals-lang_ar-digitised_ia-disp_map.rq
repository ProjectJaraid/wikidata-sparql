#title: The global Arabic periodical press until 1948: digitised titles in the Internet Archive
#defaultView:Map{"hide":["?coords"], "markercluster":"true"}
PREFIX medium: <http://www.wikidata.org/entity/Q1002697> # set publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
PREFIX lang: <http://www.wikidata.org/entity/Q13955>     # set publication language: Arabic
SELECT DISTINCT
  ?coords
  (URI(str(?periodical)) as ?item) # link to wikidata item
  (CONCAT(?periodicalLabel, ": ", ?periodicalDesc) as ?itemLabel) # label for the item
WITH {
  SELECT DISTINCT
    ?periodical
  WHERE {
    hint:SubQuery hint:runOnce true .    # this might save some time
    VALUES ?dateOfInterest {"1949-01-01"^^xsd:dateTime }. # set a date of interest
    ?periodical wdt:P31/wdt:P279* medium: ;              # limit to medium
              (wdt:P571 | wdt:P580) ?dateOnset.
    ?periodical wdt:P407/wdt:P279* lang: .            # limit by publication language (Arabic = Q13955)
    FILTER(?dateOnset < ?dateOfInterest).                # published before a specific date
  }
  ORDER BY ?dateOnset
  LIMIT 6000
} as %periodicals
# found on the internet archive
WITH {
  SELECT DISTINCT
    ?periodical
  WHERE {
    INCLUDE %periodicals
    ?periodical wdt:P195 wd:Q461.
    }
} as %digitised
WHERE {
    INCLUDE %digitised
    OPTIONAL {
      ?periodical wdt:P291 ?pubPlace. 
      ?pubPlace wdt:P625 ?coords.}
    SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
        ?periodical rdfs:label ?periodicalLabel;
            schema:description ?periodicalDesc.
    }
}
ORDER BY ?dateOnset

# URL https://query-chest.toolforge.org/redirect/wnywBGxzyyiukWWYiIQqi0smeUCu6AggKG4mOME68i3 