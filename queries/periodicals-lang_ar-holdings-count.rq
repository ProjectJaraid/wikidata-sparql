#title:No. of holdings for Arabic periodicals
#defaultView:Table
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?periodical ?periodicalLabel 
  (count(?periodical) as ?countCollections)
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    VALUES ?dateOfInterest {"1930-01-01"^^xsd:dateTime}. # set a date of interest
    ?periodical wdt:P31/wdt:P279* medium: ;              # limit to medium
              (wdt:P571 | wdt:P580) ?dateOnset.
    ?periodical wdt:P407/wdt:P279* wd:Q13955.            # limit by publication language (Arabic = Q13955)
    FILTER(?dateOnset < ?dateOfInterest).                # published before a specific date
  }
  ORDER BY ?dateOnset
  LIMIT 4000
} as %periodicals
WITH {
  SELECT DISTINCT
   ?periodical ?collection
  WHERE {
    INCLUDE %periodicals
    ?periodical wdt:P195 ?collection .
  }
  #LIMIT 10000
} as %holdings
WHERE {
  INCLUDE %holdings
  SERVICE wikibase:label {
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE], ar".
      ?periodical rdfs:label ?periodicalLabel;
          schema:description ?periodicalDesc.
      ?collection rdfs:label ?collectionLabel.
  }
}
GROUP BY ?periodical ?periodicalLabel
ORDER BY desc(?countCollections)

LIMIT 10000
# url: https://query-chest.toolforge.org/redirect/wnhAUtiqI4MQyW0MOsqAyOqOwSuE6iSUcU28oCUAqkY