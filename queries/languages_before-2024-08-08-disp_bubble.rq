#title: The global periodical press until 1930 (items present until 8 Aug 2024): most prevalent languages
#defaultView:BubbleChart
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type
SELECT DISTINCT
  ?lang ?langLabel (COUNT (DISTINCT ?periodical) as ?count)
WITH {
  SELECT DISTINCT
    ?periodical
  WHERE {
    ?periodical wdt:P31/wdt:P279* medium: ;
      (wdt:P571 | wdt:P580) ?dateOnset.
    FILTER( YEAR(?dateOnset) < 1930).
    }
  LIMIT 60000
  } as %periodicals
WITH {
  SELECT DISTINCT
    ?periodical
  WHERE{
    INCLUDE %periodicals
    BIND(REPLACE(STR(?periodical), "^.*Q(\\d+)$", "$1", "i") as ?qid).  # get QID and strip Q
    FILTER(xsd:integer(?qid) <= "128782744"^^xsd:integer).               # filter for numbers smaller than Q128782744, created on 2024-08-08
    }
  } as %period
WHERE {
  INCLUDE %period # load subgraph
  ?periodical wdt:P407 ?lang.
  ?lang wdt:P31/wdt:P279* wd:Q34770.
  SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
        ?lang rdfs:label ?langLabel.
    }
}
GROUP BY ?lang ?langLabel
#HAVING(?count > 50) # set a threshold
ORDER BY DESC(?count)