#title:No. of digitised Arabic periodicals
#url: https://query-chest.toolforge.org/redirect/JwwYdMXGLosUGeSKCucwckW0gMmEqAY2Uosa2MiMYev
#defaultView:Table
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?periodical ?periodicalLabel
  (count(?periodical) as ?countDigitised)
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    VALUES ?dateOfInterest {"1930-01-01"^^xsd:dateTime }. # set a date of interest
    ?periodical wdt:P31/wdt:P279* medium: ;              # limit to medium
              (wdt:P571 | wdt:P580) ?dateOnset.
    ?periodical wdt:P407/wdt:P279* wd:Q13955.            # limit by publication language (Arabic = Q13955)
    FILTER(?dateOnset < ?dateOfInterest).                # published before a specific date
  }
  ORDER BY ?dateOnset
  LIMIT 4000
} as %periodicals
# items with digital surrogates
WITH {
  SELECT DISTINCT
   ?periodical ?url
  WHERE {
    INCLUDE %periodicals
    {?periodical p:P195[pq:P953 ?url].}       # property statement
#    UNION {?periodical p:P195[pq:P8091 ?url].}      # ark: throws of the count
    UNION {?periodical (wdt:P953 | wdt:P1184) ?url.}        # value of property
    UNION                               # the following need resolving
    {?periodical (wdt:P1844 |           # Hathi: these should have been resolved in the ?url above
                  wdt:P12239 |          # shamela: convert to URL
                  wdt:P724 |            # archive.org: convert to URL
                  wdt:P8091 |           # ark: might be resolved in ?url above
                  wdt:P12151)           # Digita BNE
    ?url.}
  }
} as %digitised
WHERE {
  INCLUDE %digitised
  SERVICE wikibase:label {
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE], ar".
      ?periodical rdfs:label ?periodicalLabel;
          schema:description ?periodicalDesc.
      ?collection rdfs:label ?collectionLabel.
  }
}
GROUP BY ?periodical ?periodicalLabel
ORDER BY desc(?countDigitised)

LIMIT 10000