#title: الصحافة العربية حتى سنة ١٩٣٠
#defaultView:Map{"hide":["?coords"], "markercluster": "true"}
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?coords #?pubPlaceLabel
  (URI( CONCAT ("https://reasonator.toolforge.org/?&q=",
    strAfter(str(?periodical), str(wd:) ))) AS ?item)     # link to item on Reasonator
  (CONCAT(?titleStr, ": ", ?periodicalDesc) AS ?itemLabel) # construct a label for the item
  ?imgPeriodical
# all periodicals before the date of interest
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    VALUES ?dateOfInterest {"1910-06-01"^^xsd:dateTime }. # set a date of interest
    ?periodical wdt:P31/wdt:P279* medium: ;              # limit to medium
              (wdt:P571 | wdt:P580) ?dateOnset.
    ?periodical wdt:P407/wdt:P279* wd:Q13955.            # limit by publication language (Arabic = Q13955)
    FILTER(?dateOnset < ?dateOfInterest).                # published before a specific date
  }
  ORDER BY ?dateOnset
  LIMIT 4000
} as %periodicals
WHERE {
    INCLUDE %periodicals
    # retrieve more properties: locations
    OPTIONAL{
      ?periodical (wdt:P291|wdt:P159|wdt:P276 ) ?pubPlace.
      ?pubPlace wdt:P625 ?coords.}
    OPTIONAL { ?periodical wdt:P18 ?imgPeriodical.}
    # get labels and descriptions
    SERVICE wikibase:label {
      bd:serviceParam wikibase:language "ar, [AUTO_LANGUAGE]".
      ?periodical rdfs:label ?periodicalLabel;
          schema:description ?periodicalDesc.
      ?pubPlace rdfs:label ?pubPlaceLabel.
    }
    # get title in publication language
    OPTIONAL{?periodical wdt:P1476 ?title.
             ?periodical wdt:P407/wdt:P218 ?pubLang.} # Only works with ISO 639-1 code (P218)
    BIND(                                      # Bind either the original title or the lable to ?titleStr
      IF(langMatches(lang(?title), ?pubLang),  # langMatch can't get around the ISO 639-1 limitations
         str(?title),
         ?periodicalLabel) AS ?titleStr).
}
ORDER BY ?dateOnset
LIMIT 5000
#short URL to query results: https://query-chest.toolforge.org/redirect/KkTnCUlWc4MI6WKwSm6CeY4GM8EGOyScEsi4Sw0oOq4