#title: الصحافة العربية حتى سنة ١٩٣٠
#defaultView:Graph
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?url ?collection ?collectionLabel ?collectionCount
  ("FFA500" AS ?rgb)
  ?periodical ?periodicalLabel
  #(YEAR(?dateOnset) as ?year)
  ?imgPeriodical
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    # BIND(wd:Q124972743 as ?periodical).
    VALUES ?dateOfInterest {"1930-01-01"^^xsd:dateTime}. # set a date of interest
    ?periodical wdt:P31/wdt:P279* medium: ;              # limit to medium
              (wdt:P571 | wdt:P580) ?dateOnset;
              wdt:P407/wdt:P279* wd:Q13955.              # limit by publication language (Arabic = Q13955)
    FILTER(?dateOnset < ?dateOfInterest).                # published before a specific date
  }
  LIMIT 4000
} as %periodicals
# items with digital surrogates
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset ?url ?collection ?catalogueId
  WHERE {
    INCLUDE %periodicals
    {?periodical p:P195
                 [pq:P953 ?url;         # value of qualifier
                  ps:P195 ?collection].}# property statement
    UNION
    {?periodical (wdt:P953 | wdt:P1184) ?url.}        # value of property
    UNION                               # the following need resolving
    {?periodical (wdt:P1844 |           # Hathi: these should have been resolved in the ?url above
                  wdt:P12239 |          # shamela: convert to URL
                  wdt:P724 |            # archive.org: conver to URL
                  wdt:P8091 |           # ark: might be resolved in ?url above
                  wdt:P12151)           # Digita BNE
    ?catalogueId.}
  }
} as %digitised
WHERE {
  INCLUDE %digitised
  OPTIONAL { ?periodical wdt:P18 ?imgPeriodical.}
  # get labels and descriptions
  SERVICE wikibase:label {
      bd:serviceParam wikibase:language "ar, [AUTO_LANGUAGE]".
      ?periodical rdfs:label ?periodicalLabel.
      ?collection rdfs:label ?collectionLabel.
    }
}
ORDER BY ?dateOnset
LIMIT 5000