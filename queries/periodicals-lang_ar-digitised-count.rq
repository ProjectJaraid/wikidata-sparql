#title:No. of digitised collections for Arabic periodicals
#defaultView:Table
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?periodical ?periodicalLabel
  #?collection
  (count(?periodical) as ?digitisedCollections)
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    VALUES ?dateOfInterest {"1930-01-01"^^xsd:dateTime}. # set a date of interest
    ?periodical wdt:P31/wdt:P279* medium: ;              # limit to medium
              (wdt:P571 | wdt:P580) ?dateOnset.
    ?periodical wdt:P407/wdt:P279* wd:Q13955.            # limit by publication language (Arabic = Q13955)
    FILTER(?dateOnset < ?dateOfInterest).                # published before a specific date
  }
  ORDER BY ?dateOnset
  LIMIT 4000
} as %periodicals
# items in collections with digital surrogates.
WITH {
  SELECT DISTINCT
   ?periodical ?collection # we want to count the no of collections by proxy.
  WHERE {
    INCLUDE %periodicals
    {?periodical p:P195
                 [pq:P953 ?url;
                 ps:P195 ?collection].}       # property statement

  }
} as %digitised
WHERE {
  INCLUDE %digitised
  SERVICE wikibase:label {
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE], ar".
      ?periodical rdfs:label ?periodicalLabel.
  }
}
GROUP BY ?periodical ?periodicalLabel
ORDER BY desc(?digitisedCollections)

LIMIT 4000

#URL of results: https://query-chest.toolforge.org/redirect/RAvRJunsMyIeGyeeIaCo6cCukCi6YCkik8sSIkGkAq9