#title: The global periodical press until 1930 (items modified within the last week)
#defaultView:Map{"hide":["?coords"], "markercluster":"true"}
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?coords
  (URI(str(?periodical)) as ?item) # link to wikidata item
  (CONCAT(?periodicalLabel, ": ", ?periodicalDesc) as ?itemLabel) # label for the item
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    VALUES ?lang {          # select publication languages
      wd:Q13955             # Arabic
#      wd:Q36730             # Ottoman Turkish
#      wd:Q8785              # Armenian
#      wd:Q36155             # Coptic
#      wd:Q9129              # Greek
#      wd:Q9168              # Farsi
#      wd:Q36196             # Ladino
      }.
    ?periodical wdt:P31/wdt:P279* medium: ;
               (wdt:P571 | wdt:P580) ?dateOnset.
    ?periodical wdt:P407/wdt:P279* ?lang.
    FILTER( YEAR(?dateOnset) < 1930).
    }
  LIMIT 40000
  } as %periodicals
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE{
    INCLUDE %periodicals
    BIND((now() - "P7D"^^xsd:duration) AS ?lastWeek).
    ?periodical schema:dateModified ?dateModified.
    FILTER(?dateModified >= ?lastWeek).
    }
  } as %lastWeek
WHERE {
    INCLUDE %lastWeek
    OPTIONAL {
      ?periodical (wdt:P291|wdt:P159|wdt:P276|wdt:P495|wdt:P131 ) ?pubPlace.
      ?pubPlace wdt:P625 ?coords.}
    SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
        ?periodical rdfs:label ?periodicalLabel;
            schema:description ?periodicalDesc.
    }
}
ORDER BY ?dateOnset
#short URL to query results: https://w.wiki/ApcD