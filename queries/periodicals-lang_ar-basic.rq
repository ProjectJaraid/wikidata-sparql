#title: الصحافة العربية حتى سنة ١٩٣٠
#defaultView:Table
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?periodical ?periodicalLabel
  ?title (YEAR(?dateOnset) as ?year) ?pubPlaceLabel
  ?periodicalDesc
  ?inLibrary ?digitised
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    hint:SubQuery hint:runOnce true .    # this might save some time
    VALUES ?dateOfInterest {"1930-01-01"^^xsd:dateTime }. # set a date of interest
    ?periodical wdt:P31/wdt:P279* medium: ;              # limit to medium
              (wdt:P571 | wdt:P580) ?dateOnset.
    ?periodical wdt:P407/wdt:P279* wd:Q13955.            # limit by publication language (Arabic = Q13955)
    FILTER(?dateOnset < ?dateOfInterest).                # published before a specific date
  }
  ORDER BY ?dateOnset
  LIMIT 4000
} as %periodicals
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset ?inLibrary ?digitised
  WHERE {
    INCLUDE %periodicals
    OPTIONAL{
      {?periodical wdt:P195 ?collection} # this also includes the internet archive
      UNION
      {?periodical (wdt:P1042 |    # ZDB
                    wdt:P243 |     # OCLC
                    wdt:P1844 |    # Hathi
                    wdt:P6721 )    # KOBV
      ?collection.}
     }
    OPTIONAL {
      {?periodical p:P195
                 [pq:P953 ?url].}       # value of qualifier
    UNION
      {?periodical p:P195
                 [pq:P8091 ?url].}      # ark
    UNION
    {?periodical wdt:P953 ?url.}        # value of property
    UNION                               # the following need resolving
    {?periodical (wdt:P1844 |           # Hathi
                  wdt:P12239 |          # shamela
                  wdt:P724 |            # archive.org
                  wdt:P8091 |           # ark
                  wdt:P4258 |           # Gallica
                  wdt:P12151)           # Digita BNE
    ?url.}
    }
    BIND ( IF (BOUND (?collection), "x", "" )  as ?inLibrary )
    BIND ( IF (BOUND (?url), "x", "" )  as ?digitised )
  }
} as %status
WHERE {
  INCLUDE %status
     # retrieve more properties: locations
  OPTIONAL{?periodical (wdt:P291|wdt:P159|wdt:P276 ) ?pubPlace.}
  ?periodical wdt:P1476 ?title. # retrieve titles
  FILTER(LANG(?title) = 'ar'). # limit titles to Arabic strings
  # get labels and descriptions
  SERVICE wikibase:label {
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,ar,en,fr,de".
      ?periodical rdfs:label ?periodicalLabel;
          schema:description ?periodicalDesc.
      ?pubPlace rdfs:label ?pubPlaceLabel
    }
}
ORDER BY ?dateOnset ?periodical ?inLibrary ?digitised
LIMIT 6000

#URL to query results: https://query-chest.toolforge.org/redirect/5NZuEN6OBMmiWm0IoYYU6caa2q6AKeqUKEKe0uqy4Ml