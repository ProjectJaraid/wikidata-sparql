#title:Languages of periodicals published before 1930: no title in the publication language
#defaultView:BubbleChart
PREFIX medium: <http://www.wikidata.org/entity/Q1002697> # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?lang ?langLabel (COUNT (DISTINCT ?periodical) as ?count)
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    VALUES ?dateOfInterest {"1930-01-01"^^xsd:dateTime}. # set a date of interest
    ?periodical wdt:P31/wdt:P279* medium: ;              # limit to medium
              (wdt:P571 | wdt:P580) ?dateOnset.
    #FILTER("1850-01-01"^^xsd:dateTime < ?dateOnset)
    FILTER(?dateOnset < ?dateOfInterest)                # published before a specific date
  }
  LIMIT 40000
} as %periodicals
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    INCLUDE %periodicals
    # remove periodicals with title in publication language
    MINUS {
      ?periodical wdt:P1476 ?title;
                  wdt:P407/wdt:P218 ?pubLang.
      FILTER (langMatches(lang(?title), ?pubLang) )
    }
  }
} as %missingTitles
WHERE {
  INCLUDE %missingTitles
  # languages
  ?periodical wdt:P407 ?lang.
  ?lang wdt:P31/wdt:P279* wd:Q34770.
  # get labels and descriptions
  SERVICE wikibase:label {
      bd:serviceParam wikibase:language "mul,en,fr,es,de,ar,[AUTO_LANGUAGE]".
      ?lang rdfs:label ?langLabel.
    }
}
GROUP BY ?lang ?langLabel
#HAVING(?count > 50) # set a threshold
ORDER BY DESC(?count)
LIMIT 50