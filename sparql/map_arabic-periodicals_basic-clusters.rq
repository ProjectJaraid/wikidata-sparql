#title: الصحافة العربية حتى سنة ١٩٣٠
#defaultView:Map{"hide":["?coords"], "markercluster": "true"}
SELECT DISTINCT
  ?coords ?pubPlaceLabel
  (URI(str(?periodical)) as ?item) # link to the wikidata item
  (CONCAT(?periodicalLabel, ": ", ?periodicalDesc) as ?itemLabel) # construct a label for the item
WITH {
  SELECT DISTINCT
    ?periodical
  WHERE {
   ?periodical wdt:P31/wdt:P279* wd:Q1002697; # get all periodicals
       wdt:P407/wdt:P279* wd:Q13955.  # limit publication language to Arabic
    # limit to titles published before a specific date
    ?periodical (wdt:P571 | wdt:P580) ?dateOnset.
    FILTER( YEAR(?dateOnset) < 1930).
    }
  ORDER BY ?dateOnset
  LIMIT 4000
  } as %periodicals
WHERE {
    INCLUDE %periodicals
    # retrieve more properties: locations
    ?periodical (wdt:P291|wdt:P159|wdt:P276|wdt:P495|wdt:P131 ) ?pubPlace.
    ?pubPlace wdt:P625 ?coords.
    # get labels and descriptions
    SERVICE wikibase:label {
        bd:serviceParam wikibase:language "ar, [AUTO_LANGUAGE]".
        ?periodical rdfs:label ?periodicalLabel;
            schema:description ?periodicalDesc.
        ?pubPlace rdfs:label ?pubPlaceLabel.
    }
}
LIMIT 5000
#short URL to query results: https://w.wiki/Bktp