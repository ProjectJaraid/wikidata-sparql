#title: The global periodical press before 1930: Publication languages of the Eastern Mediterranean
#defaultView:Map{"hide":["?coords"]}
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?coords (?langLabel AS ?layer)
  (COUNT(?periodical) AS ?count)
  ?pubPlace ?pubPlaceLabel
# all periodicals before the date of interest
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset ?lang
  WHERE {
    VALUES ?dateOfInterest {                # set a date of interest
      "1930-01-01"^^xsd:dateTime
      }.
    VALUES ?lang {          # select publication languages
      wd:Q13955             # Arabic
      wd:Q36730             # Ottoman Turkish
      wd:Q8785              # Armenian
      wd:Q36155             # Coptic
      wd:Q9129              # Greek
      wd:Q9168              # Farsi
      wd:Q36196             # Ladino
      wd:Q37733             # Judeo-Arabic
      }.
    ?periodical wdt:P31/wdt:P279* medium: ;              # set through PREFIX medium
              (wdt:P571 | wdt:P580) ?dateOnset.
    ?periodical wdt:P407/wdt:P279* ?lang.           # limit by publication language
    FILTER(?dateOnset < ?dateOfInterest).                # published before a specific date
  }
  ORDER BY ?dateOnset
  LIMIT 40000
} as %periodicals
WHERE {
  INCLUDE %periodicals
  ?periodical (wdt:P291|wdt:P159|wdt:P276 ) ?pubPlace.
  ?pubPlace wdt:P625 ?coords. # get coordinates of locations
  # get labels and descriptions
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,ar,he,en,fr".
    ?pubPlace rdfs:label ?pubPlaceLabel.
    ?lang rdfs:label ?langLabel.
  }
}
GROUP BY ?pubPlace ?pubPlaceLabel ?coords ?langLabel
ORDER BY ?count ?langLabel

#URL to results: https://query-chest.toolforge.org/redirect/uAOfzguDGgAA6Ma2yseCsOOmGOOmaq8Go6sQQ6YEMWh