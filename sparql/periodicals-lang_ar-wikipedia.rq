#title: الصحافة العربية حتى سنة ١٩٣٠
#defaultView:Table
PREFIX medium: <http://www.wikidata.org/entity/Q1002697>    # set a publication type: periodicals (wd:Q1002697), newspapers (wd:Q11032)
SELECT DISTINCT
  ?periodical ?periodicalLabel ?periodicalDesc
  (YEAR(?dateOnset) as ?year)
  ?sitelinks ?article ?lang
WITH {
  SELECT DISTINCT
    ?periodical ?dateOnset
  WHERE {
    VALUES ?dateOfInterest {                # set a date of interest
      "1930-01-01"^^xsd:dateTime
      }.
    VALUES ?lang {          # select publication languages
      wd:Q13955             # Arabic
      wd:Q36730             # Ottoman Turkish
      wd:Q8785              # Armenian
      wd:Q36155             # Coptic
      wd:Q9129              # Greek
      wd:Q9168              # Farsi
      wd:Q36196             # Ladino
      }. # set a date of interest
    ?periodical wdt:P31/wdt:P279* medium: ;              # limit to medium
              (wdt:P571 | wdt:P580) ?dateOnset;
              wdt:P407/wdt:P279* ?lang.            # limit by publication language
    FILTER( ?dateOnset < ?dateOfInterest ).                # published before a specific date
  }
  ORDER BY ?dateOnset
  LIMIT 10000
} as %periodicals
WITH {
  SELECT DISTINCT
   ?periodical ?dateOnset ?article ?lang ?sitelinks
  WHERE {
    INCLUDE %periodicals
    ?article schema:about ?periodical;          # get Wikipedia articles for the item
             schema:inLanguage ?lang.           # get the language of the article
    ?periodical wikibase:sitelinks ?sitelinks.  # get the number of sitelinks
    #FILTER( ?sitelinks >= 1  ).                 # at least one sitelink
  }
} as %articles
WHERE {
  INCLUDE %articles
  # get labels and descriptions
  SERVICE wikibase:label {
      bd:serviceParam wikibase:language "ar, [AUTO_LANGUAGE], mul".
      ?periodical rdfs:label ?periodicalLabel;
          schema:description ?periodicalDesc.
    }
}
ORDER BY desc(?sitelinks) ?periodicalLabel ?lang ?dateOnset
LIMIT 5000