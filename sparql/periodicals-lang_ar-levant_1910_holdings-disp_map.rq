#title: Periodicals published in the Eastern Mediterranean in summer 1910 with known holdings
#defaultView:Map{"hide":["?coords", "?pubPlace"], "markercluster": "true"}
SELECT DISTINCT
  ?coords
  (URI( CONCAT ("https://reasonator.toolforge.org/?&q=", STRAFTER(STR(?periodical), STR(wd:) ))) as ?item) # link to item on Reasonator
  # (URI(str(?periodical)) as ?item) # link to the wikidata item
  (CONCAT(?periodicalLabel, ": ", ?periodicalDesc) as ?itemLabel) # construct a label for the item
# all periodicals before the date of interest
WITH {
  SELECT DISTINCT
    ?periodical ?pubPlace ?dateOfInterest ?dateOnset
  WHERE {
    VALUES ?dateOfInterest {"1910-06-01"^^xsd:dateTime}.   # set a date of interest
    ?periodical wdt:P31/wdt:P279* wd:Q11032;               # newspapers
              (wdt:P571 | wdt:P580) ?dateOnset.
   FILTER(?dateOnset < ?dateOfInterest).                   # limit to titles published before a specific date
   ?periodical (wdt:P291 | wdt:P276 | wdt:P495) ?pubPlace. # retrieve more properties: locations
  }
  LIMIT 50000
} AS %periodicals
# items within a bounding box
WITH {
  SELECT DISTINCT
    ?coords ?periodical ?pubPlace ?dateOnset ?dateOfInterest
  WHERE {
    INCLUDE %periodicals
    SERVICE wikibase:box {
      ?pubPlace wdt:P625 ?coords. # get coordinates
      # limit Western and Eastern corners: Levant
        bd:serviceParam wikibase:cornerSouthWest "Point(32.11074 31.2163)"^^geo:wktLiteral.  # SW
        bd:serviceParam wikibase:cornerNorthEast "Point(37.97328 37.34235)"^^geo:wktLiteral. # NE
     }
  }
} as %area
# items in holdings
WITH {
  SELECT DISTINCT
   ?coords ?periodical ?pubPlace ?dateOnset ?dateOfInterest
   ?catalogueId ?collection
  WHERE {
    INCLUDE %area
    # holdings: P1042=ZDB, P243=OCLC, P1844=Hathi, P6721=KOBV
    {?periodical (wdt:P1042 | wdt:P243 | wdt:P1844 | wdt:P6721) ?catalogueId.}
    UNION
    {?periodical wdt:P195 ?collection}
  }
} as %holdings
WHERE {
    INCLUDE %holdings
    # exclude discontinued papers
    OPTIONAL{ ?periodical (wdt:P582) ?dateTerminus.} 
    FILTER(!bound(?dateTerminus) || ?dateTerminus > ?dateOfInterest).
    # get labels and descriptions
    SERVICE wikibase:label {
        bd:serviceParam wikibase:language "ar, [AUTO_LANGUAGE]".
        ?periodical rdfs:label ?periodicalLabel;
            schema:description ?periodicalDesc.
    }
}
ORDER BY ?dateOnset ?periodicalLabel