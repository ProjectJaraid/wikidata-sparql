#title: الصحافة العربية في فلسطين حتى سنة ١٩٣٠
#defaultView:Map{"hide":["?coords"]}
SELECT DISTINCT
  ?coords
  (URI(str(?periodical)) as ?item) # link to the wikidata item
  (CONCAT(?periodicalLabel, ": ", ?periodicalDesc) as ?itemLabel) # construct a label for the item
WITH {
  SELECT DISTINCT
    ?periodical ?pubPlace
  WHERE {
   ?periodical wdt:P31/wdt:P279* wd:Q1002697; # get all periodicals
       wdt:P407/wdt:P279* wd:Q13955.  # limit publication language to Arabic
    # limit to titles published before a specific date
    ?periodical (wdt:P571 | wdt:P580) ?dateOnset.
    FILTER( YEAR(?dateOnset) < 1930).
    ?periodical (wdt:P291|wdt:P159|wdt:P276|wdt:P495|wdt:P131 ) ?pubPlace. # retrieve more properties: locations
    }
  ORDER BY ?dateOnset
  LIMIT 4000
  } as %periodicals
WHERE {
    INCLUDE %periodicals
    # query for values around a certain point
    wd:Q1218 wdt:P625 ?center # Jerusalem        
    SERVICE wikibase:around {
      ?pubPlace wdt:P625 ?coords. # get the coordinates of publication places
      # limit to radius around a center in km
      bd:serviceParam wikibase:center ?center. 
      bd:serviceParam wikibase:radius "150". 
     }
    # get labels and descriptions
    SERVICE wikibase:label {
        bd:serviceParam wikibase:language "ar, [AUTO_LANGUAGE]".
        ?periodical rdfs:label ?periodicalLabel;
            schema:description ?periodicalDesc.
    }
}
LIMIT 200